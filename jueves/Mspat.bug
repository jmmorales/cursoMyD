 
model{
	for (i in 1:M) { # total de bichos, incluyendo los ceros
		for (j in 1:R) { # total de trampas 
			D2[i,j] <- pow(Sx[i]-x[j,1], 2) + pow(Sy[i]-x[j,2], 2)
			# D2 es la distancia entre el centro de actividad (Sx,Sy) y la trampa j
			K[i,j] <- exp(-D2[i,j]/sigma2) # gaussian kernel
			gamma[i,j] <- K[i,j]/E[i] # pr de captura en la trampa j dado que el individuo i es capturado
			}
			
		z[i] ~ dbern(psi)
		E[i] <- sum(K[i, ]) # grado de exposición del individuo i
		n[i] ~ dbin(Paug[i], J) # conexión con los datos
		Paug[i] <- p[i]*z[i]
		p[i] <- p0 * exp(-1/E[i]) # probabilidad de captura para el individuo i
		Sx[i] ~ dunif(xlower, xupper) 
		Sy[i] ~ dunif(ylower, yupper) 
		}
	
	for(h in 1:Ncap) { # total de individuos capturados
		H[h,1] ~ dcat(gamma[H[h,2], ]) 
		#H[h,1] indica la trampa donde fue capturado el individuo H[h,2]
		}
	psi~ dunif(0, 1)
	p0 ~ dunif(0, 1)
	sigma2 ~ dunif(0, 500)
	N <- sum(z[1:M])
	D <- N/( (xupper-xlower) * (yupper-ylower) )
	}				
